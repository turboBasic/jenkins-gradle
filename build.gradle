plugins {
    id 'groovy'
    id 'com.aoe.jenkins-job-dsl' version '2.9.0'
}

repositories {
    jcenter()
    maven { url 'https://repo.jenkins-ci.org/releases/' }
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src', 'vars']
        }
        resources {
            srcDirs = ['resources']
        }
    }
    test {
        groovy {
            srcDirs = ['test']
        }
    }
}

dependencies {
    implementation "org.codehaus.groovy:groovy-all:2.4.12"
    implementation platform("org.jenkins-ci.main:jenkins-bom:${jenkinsVersion}")

    // Spock 1.3
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.4'
    // allows mocking of classes (in addition to interfaces)
    testRuntimeOnly 'net.bytebuddy:byte-buddy:1.9.12'
    // allows mocking of classes without default constructor (together with CGLIB)
    testRuntimeOnly 'org.objenesis:objenesis:3.0.1'

    // JUnit 4 + Vintage Engine 5
    testImplementation 'junit:junit'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.4.1'

    // And finally Jenkins Pipeline Unit testing framework
    testImplementation 'com.lesfurets:jenkins-pipeline-unit:1.1'
    testImplementation 'org.jenkins-ci.plugins.workflow:workflow-cps:2.5'
}

jobDsl {
    sourceDir 'src/jobs'
    resourceDir 'src/configs'
    version = "${jobDslVersion}"
    addRepositories = false
}

test {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}


/*

    The framework we are going to add now is called pipelineUnit and is only available as Groovy source files,
    so they need to be copied to your source tree. We are going to use a fork of the original project
    (https://github.com/Swaptor/pipelineUnit)

    The following files need to be copied into the project, at test/groovy/testSupport/:

    PipelineSpockTestBase.groovy
    PipelineTestHelper.groovy
    WhenExitException.groovy


 */
